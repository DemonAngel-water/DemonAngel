[toc]

# 1 中断

中断是一个过程，是指CPU在执行当前程序的过程中插入了另外一段程序的执行过程

## 1.1 中断源

```
exynos4412支持160个中断源，这些中断源分成三类：
	SGI：软中断，用于各个核之间的通信
	PPI：私有外部中断，为每个核私有的外部中断
	SPI：公共外部中断，为每个核共有的外部中断
```

## 1.2 类型

### 1.2.1 硬件中断

因为硬件故障导致的中断，硬件中断不可控

### 1.2.2 软件中断

由程序发起的中断，是预先设计好的，是可控的

## 1.3 人机交互三种方式

- 设置set
- 获取get
- 告警

## 1.4 软中断

通过软件的方式发起的中断

### 1.4.1 目标

设计一个软中断，分析软中断的处理流程

### 1.4.2 分析

#### 1.4.2.1 产生中断的方式

```
软件 ---> SWI 中断号
SWI 88  --> 用户指令 usr模式下使用
```

#### 1.4.2.2 哪里能接收软中断

==**异常向量表是在一个特殊位置上的一段代码**==

特殊位置：==**位于程序的最开始**==(一般是0x00000000)

一段代码：是由8条==**跳转指令(不一定是b，但一定是跳转指令)**==组成的程序语句块，并且==**顺序是固定**==的

| 表示                  | 含义       | 地址 |
| --------------------- | ---------- | ---- |
| Reset                 | 复位异常   | 0x00 |
| Undefined Instruction | 未定义异常 | 0x04 |
| Software Interrupt    | 软中断异常 | 0x08 |
| Prefetch Abort        | 存取异常   | 0x0c |
| Data Abort            | 数据异常   | 0x10 |
| Reserved              | 预留异常   | 0x14 |
| IRQ                   | 普通中断   | 0x18 |
| FIQ                   | 快速中断   | 0x1c |

#### 1.4.2.3 中断处理

1. 保存现场
2. 处理中断
3. 恢复现场

##### 1.4.2.3.1 保存现场

保存原模式下寄存器的值到栈，STMFD压栈

```
当SWI 88执行时，ARM core执行了下列操作
    1. 拷贝CPSR到SPSR
    2. 重新设置CPSR的值
    3. 保存SWI的下一条指令地址到LR中
    4. 设置PC的值为0x00000008(b swi_handle)
    5. user模式切换到SCV模式
    
注意：这些操作只能在ARM态执行
```

##### 1.4.2.3.2 处理中断

```
1.得到中断号
	LR - 4 ---> swi的地址 ---> swi的指令机器码 ---> 取该指令机器码的低24bit
2.比较是不是88号中断
	是，处理中断，然后返回；不是，不处理，直接到下一步
```

##### 1.4.2.3.3 恢复现场

LDMFD出栈

```
当处理完中断，恢复现场时，ARM core执行了下列操作：
	1. 将SPSR拷贝给CPSR
	2. 将SWI的下一条指令地址(LR)拷贝给PC
	3. SVC模式切换到user模式
	
注意：这些操作只能在ARM态执行
```

#### 1.4.2.4 异常优先级

异常指定了优先级和服务顺序

| 优先级 | 英文                  | 异常         |
| ------ | --------------------- | ------------ |
| 1      | Reset                 | 复位异常     |
| 2      | Data Abort            | 数据异常     |
| 3      | FIQ                   | 快速中断异常 |
| 4      | IRQ                   | 普通中断异常 |
| 5      | Prefetch Abort        | 存取异常     |
| 6      | SWI                   | 软中断异常   |
| 7      | Undefined instruction | 未定义异常   |

```assembly
.text
.global _start
_start:

b reset				@ reset
nop					@ undefined
b swi_handle		@ swi
nop					@ prefetch
nop					@ data abort
nop					@ reserved
nop					@ irq
nop					@ fiq

reset:
	@ 设置特权模式下的栈尾
	ldr sp,=stackend
	@ 从特权模式切换到用户模式
	mrs r0,cpsr
	bic r0,#0x1F
	orr r0,#0x10
	msr cpsr,r0

@ test
mov r0,#0xFF
ldr r8,=0x11111111
ldr r12,=0x22222222

swi 88				@ 用户指令，只能在用户模式下使用，切换为SVC模式
mov r1,#0xbb		@ 该指令地址会自动保存在LR中
nop
nop

swi_handle:
@ 压栈
stmfd sp!,{r0-r12,lr}
@ get interrupt number
@ LR减四就是swi指令，将swi指令放入r0中
sub r0,lr,#4
@ 将swi的指令机器码放入r1中
ldr r1,[r0]
@ 将swi高8位清0
bic r1,#0xFF000000

@ compare interrupt number
cmp r1,#88
@ 相等执行func函数
bleq func
cmp r1,#99
bleq func1

ldr r0,=0xAA
ldr r8,=0x33333333
ldr r12,=0x55555555

@ 出栈，加^切换到用户模式
ldmfd sp!,{r0-r12,pc}^

func:
	mov r1,#0x88
	@ 将SWI的下一条指令地址(LR)拷贝给PC
	mov pc,lr
	
func1:
	nop
	nop
	
.data
	stackstart:
		.space 160
	stackend:

.end
```

# 2 C和汇编的混合编程

## 2.1 如何实现混合编程

函数调用

```
start.s ---> 汇编		可以使用C函数
main.c ---> C		  可以使用汇编函数
```

## 2.2 汇编调C

### 2.2.1 如何调用

```
B/BL 函数名
```

### 2.2.2 如何传参

```
使用寄存器r0，r1，r2，r3寄存器和栈
```

### 2.2.3 如何接收返回值

```
r0(32bit)
r0和r1(64bit)
```

```assembly
.text
.global _start
_start:

ldr sp,=bufend

mov r0,#6
mov r1,#3

bl func
mov r2,#8
nop

.data
	buf:
		.space 160
	bufend:

.end
```

```c
int func(int a, int b)
{
	return a + b;
}
```

## 2.3 C调汇编

### 2.3.1 如何调用

```
变量 = 汇编函数名(参数1, 参数2......)
```

### 2.3.2 如何传参

```
实参可以用变量或数组 --> r0, r1......
```

### 2.3.3 如何接收返回值

```
变量 --> r0

return 5; ==> mov r0,#5 
			 mov pc, lr
```

```assembly
.text
.global _start
_start:

ldr sp,=bufend

mov r0,#6
mov r1,#3

bl func
mov r2,#8
nop

b main

@ 类似于函数
.global mycopy
mycopy:
loop:
	@ 将内存中的一个字节拷贝寄存器中
	ldrb r2,[r0],#1
	@ 将寄存器中的一个字节拷贝到内存中
	strb r2,[r1],#1
	cmp r2,#0
	bne loop
	mov r0,#8
	mov pc,lr
	
.data
	buf:
		.space 160
	bufend:


.end
```

```c
extern int mycopy(char *psrc, char *pdest);

int func(int a,int b)
{
	return a+b;
}

int main()
{
	char *src="hello";
	char buf[10]={0};
	int ret=0;
	ret=mycopy(src,buf);
	return ret;
}
```

